apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'war'
apply plugin: 'checkstyle'

// version = '1.0' // versionをつけると、生成されるwarの末尾にversionが付く
def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each {
  it.options.encoding = defaultEncoding
}
tasks.withType(GroovyCompile).each {
  it.groovyOptions.encoding = defaultEncoding
}
[compileJava, compileTestJava].each {
  it.options.compilerArgs += ['-source', '1.8', '-target', '1.8']
}

repositories {
  jcenter()
}

dependencies {
  compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.25.1'
  compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.25.1'
  compile group: 'io.swagger', name: 'swagger-jersey2-jaxrs', version: '1.5.17'
  compile group: 'org.hibernate', name: 'hibernate-core', version: '5.3.2.Final'

  compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4'
  compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

  testCompile group: 'junit', name: 'junit', version: '4.12'
  // JNDIをテストコンパイル時にも使用するため
  testCompile group: 'org.apache.tomcat', name: 'tomcat-catalina', version: '8.5.32'

}

// enclosed testが2回実行されることを防ぐ
test {
  exclude '**/*$*'
}

javadoc {
  options.charSet = defaultEncoding
}

// テストディレクトリはcheck対象としない
checkstyle {
  checkstyleTest.source = {}
  configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
  toolVersion = '8.0'
}

war {
  archiveName 'taskr-tomato-api.war'
}

/**
 * Hibernateの仕様でpersistance.xmlと同じディレクトリにclassesのファイルがないと参照エラーになる
 * 下記のような対処方法の他、Hibernateの普通の使い方と外れるが、src/main以下にMETA-INF/persistance.xmlを置く方法も考えられる
 * ただし、現時点のこの対処方法はGradle4.x系でしか有効ではなく、ビルドの度に次のような警告が表示される
 * Configure project :
 * Gradle now uses separate output directories for each JVM language, but this build assumes a single directory for all classes from a source set
 * This behaviour has been deprecated and is scheduled to be removed in Gradle 5.0
 * java - Gradle [META-INF/persistence.xml] was not found - Stack Overflow https://stackoverflow.com/questions/21967952/gradle-meta-inf-persistence-xml-was-not-found
 * java - Hibernate with Tomcat and gradle - Stack Overflow https://stackoverflow.com/questions/22972947/hibernate-with-tomcat-and-gradle
 * Upgrade to gradle 4 need new hibernate fix for deprecated msg - Help/Discuss - Gradle Forums https://discuss.gradle.org/t/upgrade-to-gradle-4-need-new-hibernate-fix-for-deprecated-msg/23129
*/
sourceSets {
  java {
    main {
      output.resourcesDir = output.classesDir
    }
  }
}

